#!/usr/bin/gawk -f

#
# fasta2dir - convert a FASTA file to a directory, 1 entry/file
#
# The integrity checking of the input file is as follows (in this order):
#
#	* entry names are truncated to at most LEN characters
#	  (no truncation if LEN=0);
#
#	* bad characters i.e. [^+,\-\.0-9A-Z_a-z] in entry names are
#	  replaced by '_';
#
#	* in case of non-unique entry names only the last entry is kept;
#
#	* comments are stripped.
#

BEGIN { if (OUTDIR=="") {		# mandatory: directory to print to
	   print "fasta2dir: no OUTDIR provided";
	   exit;
	}

	if (system("ls -d " OUTDIR " >/dev/null 2>&1"))
	   system("mkdir " OUTDIR);

	LEN+=0;				# allowed entry name length
	ecount = 0;			# entry count
}

/^>/ {	if (outfile!="")		# close the previous file, if any
	   close(outfile);

	ecount++;

	if (LEN)			# truncate entry name
	   name = substr($1,2,LEN);
	else
	   name = substr($1,2);

	if (name=="")			# construct unique name
	   name = "seq." ecount;
	else				# replace bad characters
	   gsub("[^+,-.0-9A-Z_a-z]","_",name);

	outfile = (OUTDIR "/" name);
	print ">" name >outfile;
	next;

}

{ if (outfile!="") print $0 >outfile; }
